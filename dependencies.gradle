ext {

    rx_java_version = '2.2.15'
    rx_android_version = "2.1.1"
    rx_kotlin_version = "2.4.0"

    retrofit_version = '2.6.1'
    okhttp_version = '4.2.1'
    gson_version = '2.8.6'

    lifecycle_version = '2.1.0'

    dagger_version = '2.25.2'
    kotlin_version = '1.3.61'
    gradle_version = '3.5.2'

    junit_version = "4.12"
    mockito_kotlin_version = '2.2.0'
}

def enableKotlin(project) {
    project.configure(project) {
        apply plugin: 'kotlin-android'
        apply plugin: 'kotlin-kapt'
        apply plugin: 'kotlin-android'
        apply plugin: 'kotlin-android-extensions'
    }

    project.dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlin_version}"
        testImplementation "org.jetbrains.kotlin:kotlin-test-junit:${kotlin_version}"
    }
}

def enableDataBinding(project) {
    project.configure(project) {
        if (project.hasProperty("android")) {
            project.android.dataBinding.enabled = true
        }
    }
}

def enableDagger(project) {
    project.dependencies {
        implementation "com.google.dagger:dagger:$dagger_version"
        implementation "com.google.dagger:dagger-android:$dagger_version"
        implementation "com.google.dagger:dagger-android-support:$dagger_version"
        kapt "com.google.dagger:dagger-android-processor:$dagger_version"
        kapt "com.google.dagger:dagger-compiler:$dagger_version"
    }
}

def enableTest(currentProject) {
    currentProject.configure(currentProject) {
        if (currentProject.hasProperty("android")) {
            android {
                testOptions {
                    unitTests.returnDefaultValues = true
                }
            }
        }
    }

    currentProject.dependencies {
        testImplementation "junit:junit:${junit_version}"
        testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$mockito_kotlin_version"
        testImplementation "android.arch.core:core-testing:$lifecycle_version"
    }
}

ext.enableKotlin = this.&enableKotlin
ext.enableDataBinding = this.&enableDataBinding
ext.enableDagger = this.&enableDagger
ext.enableTest = this.&enableTest
